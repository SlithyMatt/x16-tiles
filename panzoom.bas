1 REM PAN AND ZOOM EITHER LAYER, WITH AT LEAST LAYER 0 ENABLED
10 VPOKE $F,$2000,(VPEEK($F,$2000) OR $01)
100 PRINT ("ENTER LAYER: "): INPUT LR
150 LR = $2000 + $1000 * (LR AND 1)
200 VPOKE $F,LR,(VPEEK($F,LR) OR $01)
4200 GET A$
4250 IF A$="" THEN GOTO 4200
4300 H = VPEEK($F,LR+6) + VPEEK($F,LR+7)*$100
4400 V = VPEEK($F,LR+8) + VPEEK($F,LR+9)*$100
4410 S = VPEEK($F,$0001)
4420 SM = 128/S
4500 IF A$=CHR$(17) AND V<$FFF THEN GOSUB 7000
4600 IF A$=CHR$(29) AND H<$FFF THEN GOSUB 5000
4700 IF A$=CHR$(145) AND V>0 THEN GOSUB 8000
4800 IF A$=CHR$(157) AND H>0 THEN GOSUB 6000
4810 IF A$="+" AND S>2 THEN VPOKE $F,$0001,S/2: VPOKE $F,$0002,S/2
4820 IF A$="-" AND S<128 THEN VPOKE $F,$0001,S*2: VPOKE $F,$0002,S*2
4830 S = VPEEK($F,$0001)
4840 SM = 128/S
4900 GOTO 4200

4999 REM SCROLL RIGHT
5000 VPOKE $F,LR+6,(H+1) AND $FF
5100 VPOKE $F,LR+7,((H+1) AND $0F00)/$100
5200 RETURN

5999 REM SCROLL LEFT
6000 VPOKE $F,LR+6,(H-1) AND $FF
6100 VPOKE $F,LR+7,((H-1) AND $0F00)/$100
6200 RETURN

6999 REM SCROLL DOWN
7000 VPOKE $F,LR+8,(V+1) AND $FF
7100 VPOKE $F,LR+9,((V+1) AND $0F00)/$100
7200 RETURN

7999 REM SCROLL UP
8000 VPOKE $F,LR+8,(V-1) AND $FF
8100 VPOKE $F,LR+9,((V-1) AND $0F00)/$100
8200 RETURN

